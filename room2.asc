// Hausflur oben (Familie Hoagie)

/****************************
*   Raum-Funktionen
****************************/
function room_Load()
{
	oTuerBad.init_object(3);
	oTuerHarry.init_object(2);
	//oTuerHoagie.init_object(1);
	Door[0].Init(oTuerHoagie, hTuerHoagie);

	if ( GetGlobalInt(12) )
	{
		oLeiter.Visible = true; // offen
	}
	else
	{
		oLeiter.Visible = false; // zu
	}

	PlayMusic(24);
}


/****************************
*   Tür-Hotspot-Funktionen
****************************/
function hTuerHoagie_AnyClick()
{
	//if ( any_click_on_door(1, oTuerHoagie, 456, 138, eDirRight, 1, 35, 138, eDirRight) == 0 ) Unhandled();
	if ( Door[0].Click() == 0 ) Unhandled();
}

function hTuerHarry_AnyClick()
{
	if ( any_click_on_door(2, oTuerHarry, 105, 126, eDirUp, 4, 293, 124, eDirLeft) == 0 ) Unhandled();
}

function hTuerBad_AnyClick()
{
	if ( any_click_on_door(3, oTuerBad, 211, 126, eDirUp, 5, 278, 122, eDirLeft) == 0 ) Unhandled();
}


/****************************
*   Objekt-Funktionen
****************************/
function oLeiter_AnyClick()
{
	if (any_click_move(167, 128, eDirLeft))
	{
		if (UsedAction(eActWalkTo))
		{
			Wait(5);
			player.EnterRoom(11, 120, 126, eDirLeft);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Hier geht's auf den Dachboden.");
		}
		else if (UsedAction(eActClose))
		{
			SetGlobalInt(12, 0);
			oLeiter.Visible = false;
		}
		else Unhandled();
	}
}

function oLukenoeffner_AnyClick()
{
	if (any_click_move(83, 127, eDirLeft))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Unser Lukenöffner.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			oLukenoeffner.Visible = false;
			PlaySound(4);
			player.AddInventory(iLukenoeffner);
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Ich sollte ihn nehmen, bevor ich ihn benutzen kann.");
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose))
		{
			Wait(5);
			player.Say("Dir geht's schon noch ganz gut, wie?");
		}
		else if (UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Dann fällt er nur um.");
		}
		else Unhandled();
	}
}


/****************************
*   Hotspot-Funktionen
****************************/
function hTreppe_AnyClick()
{
	if (any_click_move(272, 122, eDirRight))
	{
		if (UsedAction(eActWalkTo) || UsedAction(eActUse))
		{
			Wait(5);
			player.EnterRoom(3, 295, 47, eDirDown);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Sie führt nach unten.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.Say("Es sind zwar ein paar Bretter lose; aber die brauche ich nicht.");
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose) || 
						 UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Du spinnst wohl!");
		}
		else Unhandled();
	}
}

function hDachluke_AnyClick()
{
	if (any_click_move(167, 128, eDirLeft))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Hier geht's zum Dachboden.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.Say("Ich komme nicht ran.");
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Wenn ich was zum öffnen hätte, könnte ich das damit versuchen.");
		}
		else if (UsedAction(eActOpen))
		{
			Wait(5);
			player.Say("Ich komme nicht ran.");
		}
		else if (UsedAction(eActClose))
		{
			Wait(5);
			player.Say("Die Luke ist schon zu.");
		}
		else if (UsedAction(eActPull) && 1 == player.InventoryQuantity[iLukenoeffner.ID])
		{
			Wait(5);
			player.Say("Klasse, mit Lukenöffner geht es.");
			PlaySound(47);
			oLeiter.Visible = true;
			SetGlobalInt(12, 1);
			RemoveWalkableArea(3);
		}
		else if (UsedAction(eActPull))
		{
			player.Say("Ich komme nicht ran.");
		}
		else if (UsedAction(eActPush))
		{
			Wait(5);
			player.Say("");
		}
		else if (UsedAction(eActUseInventory) && player.ActiveInventory == iLukenoeffner )
		{
			PlaySound(47);
			oLeiter.Visible = true;
			SetGlobalInt(12, 1);
			RemoveWalkableArea(3);
		}
		else Unhandled();
	}
}

function hBlumenvase_AnyClick()
{
	if (any_click_move(167, 128, eDirUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Eine Blume in einer Vase.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.Say("Die steht doch da ganz gut.");
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Ich mag keine Blumen.");
			Wait(10);
			player.FaceDirection(eDirDown);
			player.Say("Die schmecken mir nicht.");
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose))
		{
			Wait(5);
			player.Say("Die Vase hat keinen Deckel.");
		}
		else if (UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Dann fällt sie runter.");
		}
		else Unhandled();
	}
}

function hBild_AnyClick()
{
	if (any_click_move(377, 128, eDirUp))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("Das ist ein Bild von meinem Onkel Reuben Ben.");
			Wait(10);
			player.FaceDirection(eDirDown);
			player.Say("Der ist sogar Anführer einer Motorrad-Gang namens 'Polecats'.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.Say("Ich brauch' keine Bilder.");
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Das kann man nicht benutzen.");
			Wait(5);
			player.Say("Das kann man nicht einmal ausmalen.");
		}
		else if (UsedAction(eActOpen))
		{
			Wait(5);
			player.Say("Sehen wir so reich aus, dass man glaubt, wir hätten sowas wie einen Wandsafe?");
		}
		else if (UsedAction(eActClose))
		{
			Wait(5);
			player.Say("Das hängt geschlossen ann der Wand.");
		}
		else if (UsedAction(eActPull))
		{
			Wait(5);
			ComplainAbout(eActPull);
		}
		else if (UsedAction(eActPush))
		{
			Wait(5);
			ComplainAbout(eActPush);
		}
		else Unhandled();
	}
}

function hPoster_AnyClick()
{
	if (any_click_move(118, 133, eDirLeft))
	{
		if (UsedAction(eActWalkTo))
		{
		//	Wait(5);
		}
		else if (UsedAction(eActLookAt))
		{
			Wait(5);
			player.Say("'HEAVY METEOR METAL'.");
		}
		else if (UsedAction(eActPickUp))
		{
			Wait(5);
			player.Say("Brauch' ich nicht.");
		}
		else if (UsedAction(eActUse))
		{
			Wait(5);
			player.Say("Es hängt ein ... Poster - an der Wand.");
		}
		else if (UsedAction(eActOpen) || UsedAction(eActClose))
		{
			Wait(5);
			player.Say("Das bringt mir nichts.");
		}
		else if (UsedAction(eActPull) || UsedAction(eActPush))
		{
			Wait(5);
			player.Say("Dahinter is' nichts zum Essen.");
		}
		else Unhandled();
	}
}


/****************************
*   Regionen-Funktionen
****************************/
